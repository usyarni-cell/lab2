pipeline {
    agent {
        label 'jenkins-linux-agent-TLS'
    }

    environment {
        HARBOR_REGISTRY = 'bakul.mod.gov.my' //tempat letak image
        DOCKERFILE_LOCATION = './Dockerfile'
        PATH_YAML = 'deploy-argo/argo_deploy.yaml'
        ARGOCD_SERVER = '172.26.16.127:8181'
    }

    parameters {
        choice(name: 'ACTION', choices: ['Build and Push', 'Push Only'], description: 'Choose to build a new image or push an existing one')
        string(name: 'BASE_IMAGE_NAME', defaultValue: '', description: 'Full image name with tag (e.g., testing-app/node-helloworld:v1.01)')
        string(name: 'APP_NAME', defaultValue: '', description: 'Name of the Argo CD application to sync')
        string(name: 'GIT_URL', defaultValue: '', description: 'GitHub repository URL e.g., https://github.com/amundead/test-repo.git')
    }

    stages {
        stage('Checkout') {
            steps {
                git url: "${params.GIT_URL}", branch: 'main'
            }
        }

        stage('Build and Push Docker Image') {
            when {
                expression { params.ACTION == 'Build and Push' }
            }
            steps {
                script {
                    env.IMAGE_NAME_HARBOR = "${HARBOR_REGISTRY}/${params.BASE_IMAGE_NAME}"

                    // Build the Docker image
                    sh "docker build --no-cache -t ${env.IMAGE_NAME_HARBOR} -f ${DOCKERFILE_LOCATION} ."

                    // Push the image to the Harbor registry
                    withCredentials([usernamePassword(credentialsId: 'bakul-testing-app', passwordVariable: 'HARBOR_PASSWORD', usernameVariable: 'HARBOR_USERNAME')]) {
                        sh 'echo $HARBOR_PASSWORD | docker login $HARBOR_REGISTRY -u $HARBOR_USERNAME --password-stdin'
                        sh "docker push ${env.IMAGE_NAME_HARBOR}"
                    }
                }
            }
        }

        stage('Push Only') {
            when {
                expression { params.ACTION == 'Push Only' }
            }
            steps {
                script {
                    env.IMAGE_NAME_HARBOR = "${HARBOR_REGISTRY}/${params.BASE_IMAGE_NAME}"

                    // Pull the specified image to ensure it's available
                    withCredentials([usernamePassword(credentialsId: 'bakul-testing-app', passwordVariable: 'HARBOR_PASSWORD', usernameVariable: 'HARBOR_USERNAME')]) {
                        sh 'echo $HARBOR_PASSWORD | docker login $HARBOR_REGISTRY -u $HARBOR_USERNAME --password-stdin'
                        sh "docker pull ${env.IMAGE_NAME_HARBOR}"
                    }
                }
            }
        }

        stage('Get Docker Digest and Update YAML') {
            steps {
                script {
                    // Get Docker Digest
                    def DOCKER_DIGEST = sh(script: "docker inspect --format='{{index .RepoDigests 0}}' ${env.IMAGE_NAME_HARBOR} | cut -d '@' -f 2", returnStdout: true).trim()
                    echo "Docker Digest: ${DOCKER_DIGEST}"

                    // Check if the current YAML file already has this digest
                    def yamlUpdated = sh(
                        script: "grep 'image: ${env.IMAGE_NAME_HARBOR}@${DOCKER_DIGEST}' ${PATH_YAML}",
                        returnStatus: true // Return exit code instead of throwing an error if not found
                    )

                    if (yamlUpdated == 0) {
                        echo "YAML file is already up-to-date with image: ${env.IMAGE_NAME_HARBOR}@${DOCKER_DIGEST}. Skipping commit."
                    } else {
                        // Update YAML file with the new image and digest
                        sh "sed -i 's|image:.*|image: ${env.IMAGE_NAME_HARBOR}@${DOCKER_DIGEST}|g' ${PATH_YAML}"

                        // Commit and push the updated YAML to GitLab
                        withCredentials([usernamePassword(credentialsId: 'github-credentials-ummi', passwordVariable: 'GITHUB_PASSWORD', usernameVariable: 'GITHUB_USERNAME')]) {
                            sh """
                            git config user.name '$GITHUB_USERNAME'
                            git config user.email 'usyarni@gmail.com'
                            git remote set-url origin https://$GITHUB_USERNAME:$GITHUB_PASSWORD@${params.GIT_URL.replace('https://', '')}
                            git add ${PATH_YAML}
                            git commit -m "Update image in YAML to ${env.IMAGE_NAME_HARBOR}@${DOCKER_DIGEST}" || echo "No changes to commit."
                            git push origin main || echo "Nothing to push."
                            """
                        }
                    }
                }
            }
        }

        stage('Trigger ArgoCD Sync') {
            steps {
                script {
                    withCredentials([usernamePassword(credentialsId: 'argo-ummi', usernameVariable: 'ARGOCD_USERNAME', passwordVariable: 'ARGOCD_PASSWORD')]) {
                        def token = sh (
                            script: """curl -s -X POST -k -d '{"username":"${ARGOCD_USERNAME}", "password":"${ARGOCD_PASSWORD}"}' -H 'Content-Type: application/json' https://${ARGOCD_SERVER}/api/v1/session | jq -r .token""",
                            returnStdout: true
                        ).trim()

                        def appExists = sh (
                            script: """curl -s -X GET -k -H "Authorization: Bearer ${token}" https://${ARGOCD_SERVER}/api/v1/applications | jq -r '.items[] | select(.metadata.name == "${params.APP_NAME}")'""",
                            returnStdout: true
                        ).trim()

                        if (appExists) {
                            sh """
                            curl -s -X POST -k -H "Authorization: Bearer ${token}" -H "Content-Type: application/json" https://${ARGOCD_SERVER}/api/v1/applications/${params.APP_NAME}/sync
                            """
                        } else {
                            error("Application '${params.APP_NAME}' does not exist in Argo CD.")
                        }
                    }
                }
            }
        }
    }

    post {
        always {
            echo "Pipeline execution completed."
        }
        success {
            echo "Pipeline completed successfully!"
        }
        failure {
            echo "Pipeline failed."
        }
    }
}
